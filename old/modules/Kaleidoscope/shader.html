<!DOCTYPE HTML>
<html>
	<head>
	</head>
	<body>
		<script id="vertexshader" type="x-shader/x-vertex">
			varying vec2 Vertex_UV;
			attribute vec2 a_position, a_texCoord;
			uniform vec2 u_resolution;
			void main() {
				vec2 zeroToOne = a_position / u_resolution;
				vec2 zeroToTwo = zeroToOne * 2.0;
				vec2 clipSpace = zeroToTwo - 1.0;
				gl_Position = vec4(clipSpace * vec2(1, -1), 0, 1);
				Vertex_UV = a_texCoord;
			}
		</script>

		<script id="fragmentshader" type="x-shader/x-fragment">
			precision mediump float;
			uniform sampler2D u_modVCanvas;
			uniform float sides;
			varying vec2 Vertex_UV;

			void main() {

				vec2 uv = Vertex_UV.xy;

				// normalize to the center
				uv = uv - 0.5;

				// cartesian to polar coordinates
				float r = length(uv);
				float a = atan(uv.y, uv.x);

				// kaleidoscope
				//float sides = 5.;
				float tau = 2. * 3.1416;
				a = mod(a, tau/sides);
				a = abs(a - tau/sides/2.);

				// polar to cartesian coordinates
				uv = r * vec2(cos(a), sin(a));

				// recenter
				uv = uv + 0.5;

				vec4 c = texture2D(u_modVCanvas, uv);
				gl_FragColor = c;
			}
		</script>
	</body>
</html>